#!/usr/bin/perl

use 5.012;
use warnings;

my $pkt;
if (@ARGV == 1) {
    my ($hex) = @ARGV;
    for ($hex) {
        s/0x//g;
        s/[^0-9a-fA-F]+//g;
    }
    $pkt = pack "H*", $hex;
}
else {
    my ($fmt, @args) = @ARGV;
    $pkt = pack "$fmt", map /^0/ ? oct : $_, @args;
}

$pkt =~ s/^\xb5\x62//;

my ($len) = unpack "xxv", $pkt;
if ($len == 0xffff) {
    $len = length($pkt) - 4;
    printf "Length bytes: %02x %02x\n", $len & 0xff, $len >> 8;
    substr($pkt, 2, 2) = pack "v", $len;
}
else {
    printf "Got [%u] bytes of a [%u]-byte packet.\n", 
        length $pkt, $len + 4;
}

my ($A, $B) = (0, 0);
for (split //, $pkt) {
    $A = ($A + ord) & 0xff;
    $B = ($B + $A) & 0xff;
}

printf "Checksum bytes: %02x %02x\n", $A, $B;
my $cstr = join "", map sprintf("\\x%02x", $_), 
    map(ord, split //, $pkt), $A, $B;
print qq/Full packet: "\\xb5\\x62$cstr"\n/;
